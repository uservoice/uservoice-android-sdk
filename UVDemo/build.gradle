buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    google()
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
    signingConfigs {
        release {
            storeFile file("../demo.keystore")
            storePassword ""
            keyAlias "demo"
            keyPassword ""
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        proguard {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }
}

task askForPasswords << {
  // Must create String because System.readPassword() returns char[]
  // (and assigning that below fails silently)
  def storePw = new String(System.console().readPassword("Keystore password: "))
  def keyPw  = new String(System.console().readPassword("Key password: "))

  android.signingConfigs.release.storePassword = storePw
  android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask -> 
  if (theTask.name.equals("packageRelease")) {
    theTask.dependsOn "askForPasswords"
  }
}

dependencies {
    compile 'com.android.support:support-core-utils:26.1.0'
    compile project(':UserVoiceSDK')
}
